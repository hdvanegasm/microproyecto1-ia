// Block definition (there may be more than one block per file)
FUNCTION_BLOCK quality

// Define input variables
VAR_INPUT
    size : REAL;
    red_color : REAL;
    spots: REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    quality : REAL;
END_VAR

FUZZIFY size
    TERM small := (50, 1) (68, 1) (72, 0) (90, 0); 
    TERM medium := (50, 0) (68, 0) (72, 1) (78, 1) (80, 0) (90, 0);
    TERM large := (50, 0) (78, 0) (80, 1) (90, 1);
END_FUZZIFY

FUZZIFY red_color
    TERM low_red := (0, 1) (0.2, 1) (0.4, 0) (1, 0); 
    TERM medium_red := (0, 0) (0.2, 0) (0.4, 1) (0.7, 1) (0.9, 0) (1, 0);
    TERM high_red := (0, 0) (0.7, 0) (0.9, 1) (1, 1);
END_FUZZIFY

FUZZIFY spots
    TERM mild := (0, 1) (0.1, 1) (0.2, 0) (1, 0); 
    TERM medium := (0, 0) (0.1, 0) (0.2, 1) (0.3, 1) (0.4, 0) (1, 0);
    TERM critical := (0, 0) (0.3, 0) (0.4, 1) (1, 1);
END_FUZZIFY

DEFUZZIFY quality
    TERM low := (0, 1) (4, 1) (5, 0) (10, 0);
    TERM medium := (0, 0) (4, 0) (5, 1) (7, 1) (8, 0) (10, 0);
    TERM premium := (0, 0) (7, 0) (8, 1) (10, 1);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    ACT : MIN;  // Use 'min' activation method
    ACCU : MAX; // Use 'max' accumulation method

    RULE 1 : IF size IS small AND red_color IS low_red AND spots IS mild
        THEN quality IS low;     
    RULE 2 : IF size IS medium AND red_color IS medium_red AND spots IS mild
        THEN quality IS medium;   
    RULE 3 : IF size IS large AND red_color IS high_red AND spots IS mild 
        THEN quality IS premium;    
    RULE 4 : IF size IS small AND red_color IS low_red AND spots IS medium 
        THEN quality IS low; 
    RULE 5 : IF size IS small AND red_color IS low_red AND spots IS critical 
        THEN quality IS low; 
    RULE 6 : IF size IS medium AND red_color IS medium_red AND spots IS medium 
        THEN quality IS low;     
    RULE 7 : IF size IS medium AND red_color IS medium_red AND spots IS critical 
        THEN quality IS low;  
    RULE 8 : IF size IS large AND red_color IS high_red AND spots IS critical 
        THEN quality IS low; 
    RULE 9 : IF size IS large AND red_color IS medium_red AND spots IS mild 
        THEN quality IS medium; 
    RULE 10 : IF size IS small AND red_color IS high_red AND spots IS mild 
        THEN quality IS medium; 
    RULE 11 : IF size IS large AND red_color IS low_red AND spots IS critical 
        THEN quality IS low;    
    RULE 12 : IF size IS medium AND red_color IS high_red 
        THEN quality IS medium;  
    RULE 13 : IF size IS large AND red_color IS high_red 
        THEN quality IS premium; 
    RULE 14 : IF size IS large AND red_color IS low_red 
        THEN quality IS medium;  
    RULE 15 : IF size IS large AND red_color IS medium_red 
        THEN quality IS medium; 
END_RULEBLOCK

END_FUNCTION_BLOCK